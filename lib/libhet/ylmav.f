      SUBROUTINE YLMAV(XLAT1,XLON1,XLAT2,XLON2,LMAX,YLMH,YLMT
     1   ,WK1,WK2,WK3,SAR,D)
      save
      DOUBLE PRECISION D(*),DCTH
      COMPLEX FAC,DFAC,TEMP,TEMP1,CCON
      DIMENSION SAR(*),WK1(*),WK2(*),WK3(*),YLMH(*),YLMT(*)
C
C     WK1,WK2,WK3,SAR SHOULD BE DIMENSIONED AT LEAST (LMAX+1)*4
C     D SHOULD BE DIMENSIONED AT LEAST (2*LMAX+1)**2*8
C     YLMH,YLMT ARE IN COMLEX ARRAYS OF LENGTH (LMAX+1)**2(*8)
C
      DATA RADIAN/57.2957795/
C
      CALL POLE(XLAT1,XLON1,XLAT2,XLON2,XLATP,XLONP,AZMP,DELTA)
C
      DEL=DELTA/RADIAN
      CTH=(90.-XLATP)/RADIAN
      DCTH=CTH
      CPH=XLONP/RADIAN
      PHMP=(180.-AZMP)/RADIAN
C
      LM1=LMAX+1
      SAR(1)=1.
      DO 11 IMP=2,LM1
      ARG=FLOAT(IMP-1)*DEL*.5
   11 SAR(IMP)=SIN(ARG)/ARG
C
      IND=0
      DO 10 IL1=1,LM1
      L=IL1-1
      ITLP1=2*L+1
C
      CALL LEGNDR(90./RADIAN,L,L,WK1,WK2,WK3)
C
      CALL  ROTMX2(L,L,DCTH,D,ITLP1,ITLP1)
C
      FAC=(1.,0.)
      DFAC=CEXP(CMPLX(0.,CPH))
C
      DO 20 IM=1,IL1
      IMC=IM+L
      IBIA=ITLP1*(IMC-1)
      TEMP=(0.,0.)
C
      DO 25 IC=1,ITLP1
      MP=IC-IL1
      FMP=FLOAT(MP)
      IMP=IABS(MP)+1
      SSGN=1.
      IF(MP.LT.0.AND.(MP/2)*2.NE.MP) SSGN=-1.
      ARG=FMP*PHMP
      CCON=CMPLX(0.,ARG)
      CCON=CEXP(CCON)
      TEMP=TEMP+SSGN*WK1(IMP)*SAR(IMP)*D(IBIA+IC)*CCON
   25 CONTINUE
C
      TEMP=TEMP*FAC
      TEMP1=WK1(1)*D(IL1+IBIA)*FAC
      IND=IND+1
      YLMT(IND)=REAL(TEMP)
      YLMH(IND)=REAL(TEMP1)
      IF(IM.EQ.1) GOTO 20
      IND=IND+1
      YLMT(IND)=AIMAG(TEMP)
      YLMH(IND)=AIMAG(TEMP1)
   20 FAC=FAC*DFAC
C
   10 CONTINUE
      RETURN
      END
