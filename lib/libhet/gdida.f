




      FUNCTION GDIDA(W)
      save
C    FUNCTION GDIDA RETURNS THE GROUP DELAY OF THE APPROPRIATE IDA
C    SYSTEM (INSTRUMENT AND FILTER) AS A FUNCTION OF ANGULAR FREQUENCY
C    W (RADIANS/SEC).
C     THE VALUE RETURNED IS THE DELAY, IN SECONDS. THE SIGN CONVENTION
C    USED IS THAT A NEGATIVE VALUE CORRESPONDS TO A DELAY
C     A CALL TO THIS FUNCTION FOR A PARTICULAR IDA SYSTEM MUST
C    BE PRECEDED BY THE SUBROUTINE CALLS
C
C             NUMSTA = IDANUM(STCODE)
C             NUMCHN = IDANUM(CHCODE)
C             IERR   = IDARES(NUMSTA,NUMCHN,IYR,IDY)
C     WHERE
C                STCODE IS THE STATION CODE (ALPHA)
C                CHCODE IS THE CHANNEL NAME (ALPHA)
C                NUMSTA IS THE STATION NUMBER (0 IF NO CODE FOUND)
C                NUMCHN IS THE CHANNEL NUMBER (0 IF NO CODE FOUND)
C                IYR    IS THE YEAR A.D.
C                IDY    IS THE DAY OF THE YEAR
C                IERR   IS AN ERROR CODE (-1 IF NO RESPONSE AVAILABLE
C                          FOR THAT STATION AND TIME, OTHERWISE 1).
C
C    THESE CALLS NEED BE MADE ONLY ONCE FOR EACH NEW STATION,
C    AFTER WHICH GDIDA MAY BE CALLED REPEATEDLY TO GET THE DELAY.
C
C     $$$$CALLS ONLY SYSTEM ROUTINES
C
C        D. C. AGNEW, APRIL 1983
C
      COMPLEX POLE,ZERO,S
C   COMMON BLOCK HOLDING INSTRUMENT PARAMETERS (SET BY IDARES)
      COMMON/PLZERX/CF,NP,POLE(20),NZ,ZERO(20)
C   SET NORMALIZED FREQUENCY (FOR NORMALIZING FREQUENCY OF 1 CHZ)
      F = 15.91549431*W
      S = CMPLX(0.,F)
      F2 = F**2
C  COMPUTE GROUP DELAY FROM THE DERIVATIVE OF THE POLYNOMIAL FUNCTION
 4    GDIDA = 0.
      IF(NZ.EQ.0) GO TO 6
      DO 5 I = 1,NZ
      A = REAL(ZERO(I))
      A2 = A*A
      B = (AIMAG(ZERO(I)))**2
      IF(A.EQ.0.AND.B.EQ.0.AND.F.EQ.0) GO TO 5
      IF(B.EQ.0) GDIDA = GDIDA + A/(F2+A2)
      IF(B.NE.0) GDIDA = GDIDA + (2*A*(F2+A2+B))/
     1  ((A2+B)**2 + 2*F2*(A2-B) + F2**2)
 5    CONTINUE
 6    IF(NP.EQ.0) GO TO 8
      DO 7 I = 1,NP
      A = REAL(POLE(I))
      A2 = A*A
      B = (AIMAG(POLE(I)))**2
      IF(B.EQ.0) GDIDA = GDIDA - A/(F2+A2)
 7    IF(B.NE.0) GDIDA = GDIDA - (2*A*(F2+A2+B))/
     1  ((A2+B)**2 + 2*F2*(A2-B) + F2**2)
C   NORMALIZE GROUP DELAY TO SECONDS
 8    GDIDA = GDIDA/.062831853
      RETURN
      END
