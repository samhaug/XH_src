
C$**********************************************************************
      SUBROUTINE MATRIX
      save
      DIMENSION PART(12336)
      COMMON/INTAP/ IDUM1(3),IFACT,IDUM2(2),IT1,IT2,INSTR,NTRANS,I1S,
     1I1F,I2S,I2F,NPAR,IFTWO
      COMPLEX BRESP,RESP(257),CSPEC(257)
      COMMON/AP1/ DUM1(23),DOM,DTORG,DURTN
      COMMON/IAP5/ ARR(12850)
      COMMON/IAP6/ BRESP(257,5)
      COMMON/IAP3/ CTRACE(514),STRACE(514)
      COMMON/AP2/ OBSEI(514),SYNSEI(514),PRTMTX(300),COLVEC(24),
     1B,RMS,FILL(7386)
      EQUIVALENCE (CTRACE(1),CSPEC(1)),(PART(1),ARR(515))
      EQUIVALENCE (STRACE(1),RESP(1))
      NPTC=2**NTRANS+1
      CALL FVCLR(SYNSEI,1,840)
C
C      ESTABLISH RESPONSE TRANSFORM WITH CORRECTIONS FOR
C      DURATION AND ORIGIN TIME
C
      I=1
    5 OM=DOM*FLOAT(I-1)
      ARG=OM*DURTN
      FACT=1.
      IF(ARG.NE.0.) FACT=SIN(ARG)/ARG
      ARG=-OM*DTORG
      CA=COS(ARG)*FACT
      SA=SIN(ARG)*FACT
      RESP(I)=BRESP(I,INSTR)*CMPLX(CA,SA)
      I=I+1
      IF(I.LE.NPTC) GO TO 5
C
C      FOURIER TRANSFORM, RESPONSE, TRUNCATION
C
      NP=I2S-I1S+1
      NPF=I2F-I1F+1
      LP=NPAR*(IFTWO+1)+1
      NN=1
   50 CALL FVCLR(CTRACE,1,514)
      I=1
   51 IND=(NN-1)*514+I
      IND1=I1S+I-1
      CTRACE(IND1)=ARR(IND)
      I=I+1
      IF(I.LE.NP) GO TO 51
      CALL RFOUR(CTRACE,NTRANS,1)
      I=1
   52 CSPEC(I)=CSPEC(I)*RESP(I)
      I=I+1
      IF(I.LE.NPTC) GO TO 52
      IND=(NN-1)*514+1
      CALL TAPCLA(CTRACE,ARR(IND),I1F,I2F,IT1,IT2)
      NN=NN+1
      IF(NN.LE.LP) GO TO 50
C
C      IF BOTH MINOR & MAJOR CIRCLES, SUBTRACT MINOR FROM MAJOR ARC
C      PARTIALS
C
      IF(IFTWO.EQ.0.OR.IFACT.EQ.0) GO TO 40
      N=1
   15 N1=N+NPAR
      IND=(N-1)*514+1
      IND1=(N1-1)*514+1
      CALL FVSUB(PART(IND),1,PART(IND1),1,PART(IND1),1,514)
      N=N+1
      IF(N.LE.NPAR) GO TO 15
C
C      TRANSFORM OBSERVED SEISMOGRAM INTO THE TIME DOMAIN
C
   40 CALL TAPCLA(OBSEI,CTRACE,I1F,I2F,IT1,IT2)
      CALL FSVESQ(CTRACE(I1F),1,B,NPF)
C
C      DIFFERENCE BETWEEN OBSERVED & SYNTHETIC SEISMOGRAM, RMS
C
      CALL FVMOV(ARR,1,SYNSEI,1,514)
      CALL FVSUB(SYNSEI,1,CTRACE,1,CTRACE,1,514)
      CALL FSVESQ(CTRACE(I1F),1,RMS,NPF)
      RMS=RMS/B
      IF(IFACT.EQ.0) GO TO 75
C
C      COLUMN VECTOR AND INNER PRODUCT MATRIX
C      THE ARRAYS COLVEC AND PRTMTX HAVE ALREADY BEEN ZEROED
C
      NEQ=LP-1
      KNT=0
      N=1
   10 INDN=(N-1)*514+I1F
      CALL FDOTPR(CTRACE(I1F),1,PART(INDN),1,COLVEC(N),NPF)
      I=N
   11 KNT=KNT+1
      INDI=(I-1)*514+I1F
      CALL FDOTPR(PART(INDN),1,PART(INDI),1,PRTMTX(KNT),NPF)
      I=I+1
      IF(I.LE.NEQ) GO TO 11
      N=N+1
      IF(N.LE.NEQ) GO TO 10
C
C      TRANSFORM SYNTHETIC SEISMOGRAM INTO FREQUENCY DOMAIN
C
   75 CALL RFOUR(SYNSEI,NTRANS,1)
      RETURN
      END
