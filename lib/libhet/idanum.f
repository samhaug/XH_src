      FUNCTION IDANUM(CODE,LEN)
      save
C    FOR A GIVEN STATION OR CHANNEL CODE FOR THE PROJECT IDA STATIONS,
C   RETURNS THE APPROPRIATE NUMBER FOR USE WITH THE CALIBRATION ROUTINE
C   RIDA AND LOCATION ROUTINE LIDA. CHANNELS ARE GIVEN NEGATIVE NUMBERS.
C   A VALUE OF ZERO MEANS THAT THE CODE IS NOT RECOGNIZED.
C
C      !!!!!!!!!!!!!! FORTRAN 77 VERSION !!!!!!!!!!!!!!!
C
C    CALLING PARAMETERS:
C
C      CODE   - THE STATION CODE, OR ALPHANUMERIC CHANNEL IDENTIFIER,
C   WHICH MUST BE CHARACTER*LEN.
C      LEN    - THE LENGTH OF THE CHARACTER VARIABLE CODE, WHICH MUST
C   BE LARGE ENOUGH TO HOLD THE CHARACTERS NEEDED (3 FOR THE STATION
C   CODES, 2, 3, OR 4 FOR THE CHANNEL NAMES).
C
C      THE FOLLOWING CHANNEL ID'S ARE RECOGNIZED BY THE PROGRAM
C   (THE NUMBER IN PARENTHESES IS THE VALUE RETURNED):
C
C    (-1)   B1,BP1 - BANDPASS ONE (GAIN OF 10 IN THE MODE BAND)
C    (-2)   B2,BP2 - BANDPASS TWO (GAIN OF 100 IN THE MODE BAND)
C    (-3)   MD,MDE,MODE - MODE (GAIN OF 25 IN THE MODE BAND)
C
C      THE STATION ID'S RECOGNIZED BY THE PROGRAM ARE THOSE IN THE
C   DATA STATEMENT FOR ARRAY STCODE.
C
C    TO UPDATE THE PROGRAM:
C   INCREASE THE DIMENSION OF STCODE BY 1, AND ADD THE NEW STATION CODE
C   AND THE NEW VALUE OF NSTA IN THE DATA STATEMENT.
C
C       >>>>>>>>>>>>  LAST UPDATED ON  21 JUNE 1983  <<<<<<<<<<<<<<
C
C    ********CALLS ONLY SYSTEM ROUTINES
C
C     AUTHOR - D. C. AGNEW, IGPP LA JOLLA
C
      INTEGER LEN
      CHARACTER*(*) CODE
      CHARACTER*2 CHNID2
      CHARACTER*3 STCODE,CHNID3
      CHARACTER*4 CHNID4,TWOA
      DIMENSION STCODE(24),CHNID2(3),CHNID3(3),CHNID4(3)
      DATA STCODE/ 'ALE','BDF','BJT','CAN','CMO','EIC','ERM','ESK',
     1       'GAR','GUA','HAL','KIP','KMY','MCM','NNA','PFO','RAR',
     2       'SEY','SJG','SPA','SSB','SUR','TWO','PCR'/,TWOA/'TWOA'/,
     3     CHNID2/'B1',  'B2',  'MD'/,
     4     CHNID3/'BP1', 'BP2', 'MDE'/,
     5     CHNID4/'DUMY','DUMY','MODE'/,
     5     NSTA/24/,NCHAN/3/
      IDANUM = 0
      DO 3 I=1,NSTA
      IF(INDEX(CODE,STCODE(I)).NE.0) THEN
          IDANUM = I
          RETURN
      ENDIF
 3    CONTINUE
      IF(INDEX(CODE,TWOA).NE.0) THEN
          IDANUM = 23
          RETURN
      ENDIF
      DO 5 I=1,NCHAN
      IF(INDEX(CODE,CHNID2(I)).NE.0) THEN
          IDANUM = -I
          RETURN
      ENDIF
 5    CONTINUE
      DO 7 I=1,NCHAN
      IF(INDEX(CODE,CHNID3(I)).NE.0) THEN
          IDANUM = -I
          RETURN
      ENDIF
 7    CONTINUE
      DO 9 I=1,NCHAN
      IF(INDEX(CODE,CHNID4(I)).NE.0) THEN
          IDANUM = -I
          IF(INDEX(CODE,'DUMY').NE.0) IDANUM = 0
          RETURN
      ENDIF
 9    CONTINUE
      RETURN
      END
