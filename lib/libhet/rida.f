      COMPLEX FUNCTION RIDA(W)
      save
C    FUNCTION RIDA RETURNS THE COMPLEX TRANSFER FUNCTION OF THE
C    APPROPRIATE IDA SYSTEM (INSTRUMENT AND FILTER) AS A FUNCTION OF
C    ANGULAR FREQUENCY W (RADIANS/SEC).
C     THAT IS, RIDA(W) CONVERTS GROUND MOTION TO COUNTS, AND 1./RIDA
C    CONVERTS COUNTS TO GROUND MOTION, AT THE FREQUENCY W. (TO CONVERT
C    FROM HZ TO RAD/SEC, MULTIPLY BY 2*PI).
C     A CALL TO THIS FUNCTION FOR A PARTICULAR IDA SYSTEM MUST
C    BE PRECEDED BY THE SUBROUTINE CALLS
C
C             NUMSTA = IDANUM(STCODE)
C             NUMCHN = IDANUM(CHCODE)
C             IERR   = IDARES(NUMSTA,NUMCHN,IYR,IDY)
C     WHERE
C                STCODE IS THE STATION CODE (ALPHA)
C                CHCODE IS THE CHANNEL NAME (ALPHA)
C                NUMSTA IS THE STATION NUMBER (0 IF NO CODE FOUND)
C                NUMCHN IS THE CHANNEL NUMBER (0 IF NO CODE FOUND)
C                IYR    IS THE YEAR A.D.
C                IDY    IS THE DAY OF THE YEAR
C                IERR   IS AN ERROR CODE (-1 IF NO RESPONSE AVAILABLE
C                          FOR THAT STATION AND TIME, OTHERWISE 1).
C
C    THESE CALLS NEED BE MADE ONLY ONCE FOR EACH NEW STATION,
C    AFTER WHICH RIDA MAY BE CALLED REPEATEDLY TO GET THE RESPONSE.
C
C     $$$$CALLS ONLY SYSTEM ROUTINES
C
C        D. C. AGNEW, APRIL 1983
C
      COMPLEX POLE,ZERO,S
C   COMMON BLOCK HOLDING INSTRUMENT PARAMETERS (SET BY IDARES)
      COMMON/PLZERX/CF,NP,POLE(20),NZ,ZERO(20)
      RIDA = CMPLX(CF,0.)
C   SET NORMALIZED FREQUENCY (FOR NORMALIZING FREQUENCY OF 1 CHZ)
      S = CMPLX(0.,15.91549431*W)
C   COMPUTE RESPONSE, INCLUDING COMPLEX CONJUGATES AS NEEDED
      IF(NZ.EQ.0) GO TO 2
      DO 1 I=1,NZ
      IF(AIMAG(ZERO(I)).EQ.0) RIDA = RIDA*(S+ZERO(I))
 1    IF(AIMAG(ZERO(I)).NE.0) RIDA = RIDA*(S+ZERO(I))*
     1   (S+CONJG(ZERO(I)))
 2    IF(NP.EQ.0) RETURN
      DO 3 I=1,NP
      IF(AIMAG(POLE(I)).EQ.0) RIDA = RIDA/(S+POLE(I))
 3    IF(AIMAG(POLE(I)).NE.0) RIDA = RIDA/((S+POLE(I))*
     1   (S+CONJG(POLE(I))))
      RETURN
      END
